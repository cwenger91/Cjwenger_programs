!
! ***  CALLING SEQUENCE:
!
! CALL DECOMP (NDIM,N,COND,IPVT,WORK,A)
! CALL SOLVE (NDIM,N,B,IPVT,A)
!
!
! SUBROUTINE DECOMP FACTORIZES A REAL MATRIX BY FULLY PIVOTED
! GAUSS ELIMINATION. DECOMP ALSO COMPUTES THE CONDITION NUMBER.
!
! SUBROUTINE SOLVE COMPUTES THE SOLUTIONS TO LINEAR SYSTEMS.
!
! ***  INPUTS:
!
! NDIM = DECLARED DIMENSIONS OF A(NDIM,NDIM), WORK(NDIM), AND 
!        PIVOT(NDIM) IN THE CALLING PROGRAM.
!
! N = NUMBER OF LINEAR EQUATIONS
!
! A = MATRIX TO BE TRIANGULARIZED
!
! *** OUTPUTS:
!
! A = UPPER (U) AND LOWER (L) TRIANGULARIZED MATRICES WHERE
!     A = L * U
!
! COND = CONDITION NUMBER OF MATRIX A. COND IS SET TO 
!        1.0E+32 IF EXACT SINGULARITY IS DETECTED.
!
! IPVT = THE PIVOT VECTOR. 
!        IPVT(K) = INDEX OF THE K-TH PIVOT ROW
!        IPVT(N) = (-1)**(NUMBER OF INTERCHANGES).
!
! *** WORKSPACE:
!
! WORK = MUST BE DECLARED AND INCLUDED IN THE CALL. ITS INPUT 
!        IS IGNORED, ITS OUTPUT IS UNIMPORTANT.
!
! *** DETERMINANT:
!
! THE DETERMINANT OF MATRIX A CAN BE CALCULATED AS
! DET(A) = IPVT(N)*A(1,1)*A(2,2)......*A(N,N)
!
! *** REFERENCE:
!
! Computer Methods for Mathematical Computations by 
! G.E. Forsythe, M.A. Malcolm, C.B. Moler (QA297 F568)
!
!

	SUBROUTINE DECOMP(NDIM,N,COND,IPVT,WORK,A)
	
	IMPLICIT NONE

	INTEGER :: NDIM,N
	
	REAL(KIND=8) :: WORK(NDIM),A(NDIM,NDIM)
	REAL(KIND=8) :: ANORM,T,EK,YNORM,ZNORM,COND

	INTEGER :: IPVT(NDIM),NM1,J,I,K,KP1,M
	INTEGER :: KM1,KB
	
	IPVT(N)=1
	IF(N .EQ. 1) GO TO 80
	NM1=N-1
!
! COMPUTE 1-NORM OF MATRIX A
!
	ANORM=0.0D0
	DO 10 J=1,N
		T=0.0D0
		DO 5 I=1,N
			T=T+DABS(A(I,J))
5		CONTINUE
		IF(T .GT. ANORM) ANORM=T
10 	CONTINUE

!
! GAUSSIAN ELIMINATION WITH PIVOTING
!

	DO 35 K=1,NM1
		KP1=K+1
!
! FIND PIVOT
!
		M=K
		DO 15 I=KP1,N
			IF(DABS(A(I,K)) .GT. DABS(A(M,K))) M=I
15 		CONTINUE
		IPVT(K)=M
		IF( M .NE. K ) IPVT(N)=-IPVT(N)
		T=A(M,K)
		A(M,K)=A(K,K)
		A(K,K)=T
!
! SKIP STEP IF PIVOT IS ZERO
!
		IF(T .EQ. 0.0D0) GO TO 35
!
! COMPUTE MULTIPLIERS
!
		DO 20 I=KP1,N
			A(I,K)=-A(I,K)/T
20		CONTINUE
!
! INTERCHANGE AND ELIMINATE BY COLUMNS
!
		DO 30 J=KP1,N
			T=A(M,J)
			A(M,J)=A(K,J)
			A(K,J)=T
			IF( T .EQ. 0.0D0 ) GO TO 30
			DO 25 I=KP1,N
				A(I,J)=A(I,J)+A(I,K)*T
25			CONTINUE
30		CONTINUE
35	CONTINUE
!
! CONDITION NUMBER IS CALCULATED BY SOLVING TWO SYSTEMS,
! (A-TRANSPOSE)*Y = E AND A*Z = Y. E IS A VECTOR OF +1 OR -1
! CHOSEN TO CAUSE GROWTH IN Y. 
! COND = (1-NORM OF Z)/(1-NORM OF Y)
!
! SOLVING (A-TRANSPOSE)*Y = E
!
	DO 50 K=1,N
		T=0.0D0
		IF(K .EQ. 1) GO TO 45
		KM1=K-1
		DO 40 I=1,KM1
			T=T+A(I,K)*WORK(I)
40		CONTINUE
45		EK=1.0D0
		IF(T .LT. 0.0) EK=-1.0D0
		IF(A(K,K) .EQ. 0.0D0) GO TO 90
		WORK(K)=-(EK+T)/A(K,K)
50	CONTINUE
	DO 60 KB=1,NM1
		K=N-KB
		T=0.0D0
		KP1=K+1
		DO 55 I=KP1,N
			T=T+A(I,K)*WORK(K)
55		CONTINUE
		WORK(K)=T
		M=IPVT(K)
		IF(M .EQ. K) GO TO 60
		T=WORK(M)
		WORK(M)=WORK(K)
		WORK(K)=T
60	CONTINUE
	YNORM=0.0D0
	DO 65 I=1,N
		YNORM=YNORM+DABS(WORK(I))
65	CONTINUE
!
! SOLVING A*Z = Y
!
	CALL SOLVE(NDIM,N,WORK,IPVT,A)
	ZNORM=0.0D0
	DO 70 I=1,N
		ZNORM=ZNORM+DABS(WORK(I))
70	CONTINUE
!
! ESTIMATE COND
!
	COND=ANORM*ZNORM/YNORM
	IF(COND .LT. 1.0D0 ) COND=1.0D0
	RETURN
!
! 1-BY-1 MATRIX
!
80	COND=1.0D0
	IF(A(1,1) .NE. 0.0D0) RETURN
!
! EXACT SINGULARITY
!
90	COND=1.0D+32
	RETURN
	END
	
!
! SOLUTION OF LINEAR SYSTEM A*X=B
!
! DO NOT USE IF DECOMP HAS DETECTED SINGULARITY
!
! *** INPUT:
!
! NDIM= DECLARED DIMENSIONS OF A(NDIM,NDIM), PIVOT(NDIM), AND 
!        B(NDIM) IN THE CALLING PROGRAM.
!
! N = NUMBER OF LINEAR EQUATIONS
!
! B = RIGHT HAND SIDE VECTOR
!
! IPVT = PIVOT VECTOR OBTAINED FROM DECOMP
!
! A = TRIANGULARIZED MATRIX OBTAINED FROM DECOMP
!
! *** OUTPUT:
!
! B = SOLUTION VECTOR
!
!
	SUBROUTINE SOLVE(NDIM,N,B,IPVT,A)
	
	IMPLICIT NONE
	
	INTEGER :: NDIM,N,NM1,K,KP1,M,I,KB,KM1
	INTEGER :: IPVT(NDIM)

	REAL(KIND=8) B(NDIM),A(NDIM,NDIM),T
!
! FORWARD ELIMINATION
!
	IF( N .EQ. 1) GO TO 50
	NM1=N-1
	DO 20 K=1,NM1
		KP1=K+1
		M=IPVT(K)
		T=B(M)
		B(M)=B(K)
		B(K)=T
		DO 10 I=KP1,N
			B(I)=B(I)+A(I,K)*T
10		CONTINUE
20	CONTINUE
!
! BACK SUBSTITUTION
!
	DO 40 KB=1,NM1
		KM1=N-KB
		K=KM1+1
		B(K)=B(K)/A(K,K)
		T=-B(K)
		DO 30 I=1,KM1
			B(I)=B(I)+A(I,K)*T
30		CONTINUE
40	CONTINUE
50	B(1)=B(1)/A(1,1)
	RETURN
	END

